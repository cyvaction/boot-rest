buildscript {
    repositories {
        maven { url 'http://repo.spring.io/libs-snapshot' }
        mavenLocal()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.0.0.RC3'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'

def javaVersion = 1.8
ext {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    baseName = 'boot-rest'
    version = '0.1.0'
}

jar {
    archiveName = "${baseName}.jar"
}

// Toggle to enable building WAR
// -----------------------------
def buildWar = false
//def buildWar = true
//apply plugin: 'war'
//war {
//    archiveName = "${baseName}.war"
//}
// -----------------------------

repositories {
    mavenCentral()
    maven { url 'http://repo.spring.io/libs-snapshot' }
}

// Versions
// ========
// Core
def springBootVersion = '1.0.0.RC3'
def springDataEsVersion = '1.0.0.BUILD-SNAPSHOT'
def hibernateValidatorVersion = '5.0.2.Final'
def servletApiVersion = '3.1.0'
def jettyVersion = '9.1.2.v20140210'
def springHk2BridgeVersion = '2.2.0'
def jerseyVersion = '2.5.1'
def jacksonVersion = '2.3.1'

// Utilities
def lombokVersion = '1.12.4'
def guavaVersion = '16.0.1'
def groovyVersion = '2.2.1'
def yamlVersion = '1.13'

// Testing
def junitVersion = '4.11'
def festAssertVersion = '1.4'
def mockitoVersion = '1.9.5'
def objenesisVersion = '1.4'

dependencies {
    // Core
    // ====
    // Spring Boot
    compile(group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: springBootVersion) {
        exclude module: 'spring-boot-starter-tomcat'
    }
    if (!buildWar) {
        compile group: 'org.springframework.boot', name: 'spring-boot-starter-jetty', version: springBootVersion
    }
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: springBootVersion
    compile(group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: springBootVersion) {
        exclude module: 'hsqldb'
    }
    // Spring Data Elasticsearch
    compile group: 'org.springframework.data', name: 'spring-data-elasticsearch', version: springDataEsVersion
    // Bean Validation
    compile group: 'org.hibernate', name: 'hibernate-validator', version: hibernateValidatorVersion
    // Servlet API
    compile group: 'javax.servlet', name: 'javax.servlet-api', version: servletApiVersion
    // Jetty
    compile group: 'org.eclipse.jetty', name: 'jetty-servlets', version: jettyVersion
    if (!buildWar) {
        compile group: 'org.eclipse.jetty', name: 'jetty-client', version: jettyVersion
        compile group: 'org.eclipse.jetty', name: 'jetty-continuation', version: jettyVersion
        compile group: 'org.eclipse.jetty', name: 'jetty-http', version: jettyVersion
        compile group: 'org.eclipse.jetty', name: 'jetty-io', version: jettyVersion
        compile group: 'org.eclipse.jetty', name: 'jetty-jsp', version: jettyVersion
        compile group: 'org.eclipse.jetty', name: 'jetty-security', version: jettyVersion
        compile group: 'org.eclipse.jetty', name: 'jetty-server', version: jettyVersion
        compile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: jettyVersion
        compile group: 'org.eclipse.jetty', name: 'jetty-util', version: jettyVersion
        compile group: 'org.eclipse.jetty', name: 'jetty-webapp', version: jettyVersion
        compile group: 'org.eclipse.jetty', name: 'jetty-xml', version: jettyVersion
    }
    // Jersey
    compile group: 'org.glassfish.hk2', name: 'spring-bridge', version: springHk2BridgeVersion
    compile(group: 'org.glassfish.jersey.core', name: 'jersey-common', version: jerseyVersion) {
        exclude module: 'guava'
    }
    compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet', version: jerseyVersion
    // Jackson
    compile group: 'com.fasterxml.jackson.jaxrs', name: 'jackson-jaxrs-json-provider', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: jacksonVersion

    // Utilities
    // =========
    compile group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    compile group: 'com.google.guava', name: 'guava', version: guavaVersion
    runtime group: 'org.codehaus.groovy', name: 'groovy-all', version: groovyVersion
    runtime group: 'org.yaml', name: 'snakeyaml', version: yamlVersion

    // Testing
    // =======
    testCompile group: 'junit', name: 'junit', version: junitVersion
    testCompile group: 'org.easytesting', name: 'fest-assert', version: festAssertVersion
    testCompile(group: 'org.mockito', name: 'mockito-core', version: mockitoVersion) {
        exclude module: 'objenesis'
    }
    testCompile group: 'org.objenesis', name: 'objenesis', version: objenesisVersion
    testCompile group: 'org.glassfish.jersey.test-framework.providers',
            name: 'jersey-test-framework-provider-grizzly2', version: jerseyVersion
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}

// Used by Heroku
task stage(dependsOn: 'build')
